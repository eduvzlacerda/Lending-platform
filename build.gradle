plugins {
	id 'org.springframework.boot' version '2.6.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
	id 'idea'
}

group = 'com'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

idea {
	module {
		sourceDirs -= file('src/integrationTest/java')
		testSourceDirs += file('src/integrationTest/java')
	}
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir "$projectDir/src/integrationTest/java"
		}
		resources.srcDir "$projectDir/src/integrationTest/resources"
	}
}

jacoco {
	toolVersion = "0.8.5"
}

jacocoTestReport {

	reports {
		xml.enabled false
		csv.enabled false
		html.destination file("${buildDir}/jacocoHtml")
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: 'org/**')
		}))
	}

	// TODO: per test!
	getExecutionData().setFrom(fileTree(buildDir).include("/jacoco/*.exec"))

}

tasks.withType(Test) {

	useJUnitPlatform()

	testLogging {

		showExceptions true
		exceptionFormat "full"
		showCauses true
		showStackTraces true
		//showStandardStreams = true

	}

	if(project.hasProperty('ci')) {
		systemProperty 'spring.datasource.url', 'jdbc:postgresql://postgres:5432/lab'
	}

	finalizedBy jacocoTestReport

}

task junitTest(type: Test) {

	testClassesDirs = sourceSets.test.output.classesDirs
	classpath = sourceSets.test.runtimeClasspath

}

task integrationTest(type: Test) {

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath

}

processIntegrationTestResources {
	// Somehow gradle tries to copy application.properties twice. Not sure why.
	// With this duplicatesStrategy, this bug is fixed!
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

check.dependsOn integrationTest
check.dependsOn junitTest

configurations {
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}


dependencies {

	implementation group: 'io.springfox', name: 'springfox-boot-starter', version: '3.0.0'
	implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: '3.0.0'

	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.projectlombok:lombok:1.18.18'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

	runtimeOnly 'org.postgresql:postgresql'
	compileOnly 'org.projectlombok:lombok:1.18.22'
	annotationProcessor 'org.projectlombok:lombok:1.18.22'

	testCompileOnly 'org.projectlombok:lombok:1.18.22'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
	testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
	testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.1.0'
	testImplementation 'org.apache.httpcomponents:httpclient'

}